Иногда необходимо создать реквизиты на форме 1С. Это выполняется с помощью операции:

```
ДобавляемыеРеквизиты = Новый Массив;
НовыйРеквизит = Новый РеквизитФормы(ИмяНовогоРеквизиты, Новый ОписаниеТипов(ТипНовогоРеквизита) );
ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);

ИзменитьРеквизиты(ДобавляемыеРеквизиты);
```

Гораздо интереснее задача преобразования табличной части в реквизиты формы. Для этого необходимо сделать составное имя реквизита, в котором будет указано:
- Имя табличной части;
- Номер строки;
- Имя реквизита.

```
Функция ПреобразоватьТабличнуюЧастьВРеквизиты(Знач ДобавляемыеРеквизиты, Знач ОбъектДокумента, Знач ИмяТабЧасти)
  // определяем метаданные таблицы
	МетаданныеТаблицы = ОбъектДокумента.Метаданные().ТабличныеЧасти[ИмяТабЧасти].Реквизиты;
	
  Для Каждого Стр Из ОбъектДокумента[ИмяТабЧасти] Цикл
		
		// обходим колонки
		Для Каждого Колонка Из МетаданныеТаблицы Цикл

      // здесь переопределяем тип для характеристики			
			Если Колонка.ИМя = "Значение" Тогда
				ТипЗначения = СТр.Параметр.ТипЗначенияПараметра.ТипЗначения;
			Иначе
				ТипЗначения =  Колонка.Тип;
			КОнецЕсли;
			Рекв = Новый РеквизитФормы(
			ИмяТабЧасти + "_" + СТр.НомерСтроки + "_" + Колонка.Имя, 
			ТипЗначения
			);
			
			// добавляем в перечень создаваемых реквизитов
			ДобавляемыеРеквизиты.Добавить(Рекв);
			
		КонецЦикла;
	КонецЦикла;
	Возврат ДобавляемыеРеквизиты;
КонецФункции
```

Обратная задача реализуется чуть сложнее. Необходимо сначала собрать информацию реквизитов в строки, а потом загрузить в табличную часть:

```
Функция ПреобразоватьРеквизитыВТабличнуюЧасть (знач ОбъектДокумента, Знач ИмяТабЧасти)
	СтрокиТаблицы = ПолучитьСоответствиеРеквизитов(ИмяТабЧасти);	
	// перед замещением удаляем старые записи
	ОбъектДокумента[ИмяТабЧасти].Очистить();
	Для КАждого СтрокаТаблицы Из СтрокиТаблицы Цикл
		ЗаполнитьЗначенияСвойств( ОбъектДокумента[ИмяТабЧасти].Добавить(), СтрокаТаблицы.Значение);
	КонецЦикла;
КонецФункции

// генерация списка строк, на основании реквизитов формы
Функция ПолучитьСоответствиеРеквизитов (Знач ИмяТабЧасти)
	ДлинаИмени = СтрДлина(ИмяТабЧасти);
	// для удобства объединения различных строк будем использовать Соответствие
	Таблица = Новый Соответствие;
  // получаем все реквизиты формы
	Для Каждого Рекв ИЗ ПолучитьРеквизиты() ЦИкл
		Если ЛЕв(Рекв.Имя , ДЛинаИмени) = ИмяТабЧасти Тогда
			// заполняем таблицу строк
			СоставноеПоле = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Рекв.Имя, "_");
			Стр = Таблица.Получить(СоставноеПоле[1]);
			Если Стр = Неопределено Тогда
				Стр  = Новый Структура;
				Таблица.Вставить(СоставноеПоле[1], Стр);
			КонецЕсли;
			
			Стр.Вставить(СоставноеПоле[2], Этаформа[Рекв.Имя]);
		КонецЕсли;
	КонецЦикла;

	возврат Таблица;
КонецФункции
```

Общий вызов функций может быть так выглядеть:

```
ДобРеквизиты = Новый Массив;
ПреобразоватьТабличнуюЧастьВРеквизиты(ДобРеквизиты, ДокОб, "ПараметрыИзделий");
ПреобразоватьТабличнуюЧастьВРеквизиты(ДобРеквизиты, ДокОб, "ПараметрыПартии");
ИзменитьРеквизиты(ДобРеквизиты);
	
ПреобразоватьРеквизитыВТабличнуюЧасть(ДокОб, "ПараметрыИзделий");
ПреобразоватьРеквизитыВТабличнуюЧасть(ДокОб, "ПараметрыПартии");

```
