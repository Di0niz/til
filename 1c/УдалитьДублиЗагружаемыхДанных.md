Основная идея в том, что читаем данные из регистра в который хотим записать данные, затем суммируем и получаем разницу.

1. Реализация через таблицы значений;

```BSL
Функция УдалитьДублиЗагружаемыхДанных(Знач ИмяТаблицы, Знач ТаблицаДвижений)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "Выбрать * ИЗ РегистрНакопления."+ ИмяТаблицы;
	ТаблицаСвертки = Запрос.Выполнить().Выгрузить();
	
	
	КолонкиРесурсов = Новый Массив;
	// инвертируем колонки 
	Для Каждого Поле Из Метаданные.РегистрыНакопления[ИмяТаблицы].Ресурсы Цикл
		
		КолонкиРесурсов.Добавить(Поле.Имя);
		
		КолонкаЗначений = ТаблицаСвертки.ВыгрузитьКолонку(Поле.Имя);
		
		Для Инд = 0 по КолонкаЗначений.Количество()-1 Цикл
			КолонкаЗначений[Инд] = -КолонкаЗначений[Инд];
		КонецЦикла;
		
		ТаблицаСвертки.ЗагрузитьКолонку(КолонкаЗначений,Поле.Имя);
	КонецЦикла;
	
	// дополняем таблицу
	Для Каждого Стр Из ТаблицаДвижений Цикл
		ЗаполнитьЗначенияСвойств( ТаблицаСвертки.Добавить(), Стр);
	КонецЦикла;
	
	КолонкиСвертки = Новый Массив;
	Для Каждого Поле Из Метаданные.РегистрыНакопления[ИмяТаблицы].Измерения Цикл
		КолонкиСвертки.Добавить(Поле.Имя);
	КонецЦикла;
	Для Каждого Поле Из Метаданные.РегистрыНакопления[ИмяТаблицы].Реквизиты Цикл
		КолонкиСвертки.Добавить(Поле.Имя);
	КонецЦикла;
	КолонкиСвертки.Добавить("Период");

	
	ПоляРесурсов = СтроковыеФункцииКлиентСервер.СтрокаИзМассиваПодстрок(КолонкиРесурсов);
	ПоляСвертки = СтроковыеФункцииКлиентСервер.СтрокаИзМассиваПодстрок(КолонкиСвертки);
	
	// сворачиваем таблицу 
	ТаблицаСвертки.Свернуть(ПоляСвертки,ПоляРесурсов);
	
	// в результате всертки удаляем колонки, которые имеют 0
	Инд = ТаблицаСвертки.Количество() -1;
	Пока Инд  >= 0 Цикл 
		УдалитьСтроку = Истина;
		СтрокаНаУдаление = ТаблицаСвертки[Инд];
		ДЛя Каждого КолонкаРесурса Из КолонкиРесурсов Цикл
			УдалитьСтроку = УдалитьСтроку  И СтрокаНаУдаление[КолонкаРесурса] = 0;
		КонецЦикла;
		
		Если УдалитьСТроку Тогда
			ТаблицаСвертки.Удалить(СтрокаНаУдаление);
		КонецЕсли;
		Инд = Инд -1;
		
	КонецЦикла;
	
	Возврат ТаблицаСвертки;
КонецФункции
```


2. Реализация через временные таблицы (Запрос)
